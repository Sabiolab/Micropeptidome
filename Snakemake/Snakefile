#############################################
# Snakefile: per-sample ShortStop workflow
# Uses: StringTie, TransDecoder, ShortStop
#
# Run:
#   conda activate snakemake
#   snakemake --use-conda --slurm -j 32
#
#  In the running directory there must be a config.yaml file, a GRCh38.primary_assembly.genome.fa and a gencode.v38.primary_assembly.annotation.gtf
#  Check the workflow.sh to see how to download the annotation and FASTA files.
#############################################

import glob
from pathlib import Path

configfile: "config.yaml"

# Make OUTDIR absolute to avoid SLURM working-dir surprises
OUTDIR = str(Path(config["outdir"]).resolve())

# Discover BAMs from glob and use the discovered paths (avoid bam_root reconstruction issues)
BAMS = sorted(glob.glob(config["bam_glob"]))
if not BAMS:
    raise ValueError(f"No BAMs found. Check bam_glob: {config['bam_glob']}")

# Sample name = parent directory name
SAMPLES = [Path(bam).parent.name for bam in BAMS]

# Map sample -> actual BAM path found by bam_glob
BAM_BY_SAMPLE = {}
for bam in BAMS:
    s = Path(bam).parent.name
    if s in BAM_BY_SAMPLE and BAM_BY_SAMPLE[s] != bam:
        raise ValueError(f"Multiple BAMs found for sample '{s}':\n  {BAM_BY_SAMPLE[s]}\n  {bam}\nFix bam_glob or sample naming.")
    BAM_BY_SAMPLE[s] = bam

def bam_path(wc):
    return BAM_BY_SAMPLE[wc.sample]

rule all:
    input:
        expand(f"{OUTDIR}/{{sample}}/shortstop/predict.done", sample=SAMPLES)


rule stringtie_assemble:
    input:
        bam=bam_path,
        ref_gtf=config["gencode_gtf"]
    output:
        gtf=f"{OUTDIR}/{{sample}}/stringtie/{{sample}}.gtf"
    threads: config.get("threads_stringtie", 8)
    shell:
        r"""
        set -euo pipefail
        mkdir -p "{OUTDIR}/{wildcards.sample}/stringtie"

        conda run --no-capture-output -n smORFs stringtie "{input.bam}" \
          -G "{input.ref_gtf}" \
          -o "{output.gtf}" \
          -p {threads}
        """


rule gffread_transcripts:
    input:
        gtf=f"{OUTDIR}/{{sample}}/stringtie/{{sample}}.gtf",
        genome=config["genome_fa"]
    output:
        fa=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa"
    threads: 1
    shell:
        r"""
        set -euo pipefail
        mkdir -p "{OUTDIR}/{wildcards.sample}/transcripts"

        conda run --no-capture-output -n smORFs gffread "{input.gtf}" \
          -g "{input.genome}" \
          -w "{output.fa}"
        """


rule transdecoder_longorfs:
    input:
        fa=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa"
    output:
        done=f"{OUTDIR}/{{sample}}/transdecoder/longorfs.done"
    threads: 1
    resources:
        mem_mb=16000,
        time="04:00:00"
    shell:
        r"""
        set -euo pipefail
        mkdir -p "{OUTDIR}/{wildcards.sample}/transdecoder"

        cd "{OUTDIR}/{wildcards.sample}/transcripts"
        conda run --no-capture-output -n smORFs TransDecoder.LongOrfs -t "{wildcards.sample}.transcripts.fa"

        touch "../transdecoder/longorfs.done"
        """


rule transdecoder_predict:
    input:
        fa=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa",
        longorfs_done=f"{OUTDIR}/{{sample}}/transdecoder/longorfs.done"
    output:
        pep=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa.transdecoder.pep",
        gff3=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa.transdecoder.gff3"
    threads: 1
    resources:
        mem_mb=64000,
        time="04:00:00"
    shell:
        r"""
        set -euo pipefail

        cd "{OUTDIR}/{wildcards.sample}/transcripts"
        conda run --no-capture-output -n smORFs TransDecoder.Predict -t "{wildcards.sample}.transcripts.fa"

        test -s "{wildcards.sample}.transcripts.fa.transdecoder.pep"
        test -s "{wildcards.sample}.transcripts.fa.transdecoder.gff3"
        """


rule filter_smorfs:
    input:
        pep=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa.transdecoder.pep",
        gff3=f"{OUTDIR}/{{sample}}/transcripts/{{sample}}.transcripts.fa.transdecoder.gff3",
        script=config["filter_smorf_pep_py"]
    output:
        smorfs_fa=f"{OUTDIR}/{{sample}}/smorfs/{{sample}}.smorfs.fa",
        ids=f"{OUTDIR}/{{sample}}/smorfs/{{sample}}.smorf_ids.txt",
        smorfs_gff3=f"{OUTDIR}/{{sample}}/smorfs/{{sample}}.smorfs.gff3"
    threads: 1
    shell:
        r"""
        set -euo pipefail
        mkdir -p "{OUTDIR}/{wildcards.sample}/smorfs"

        conda run --no-capture-output -n smORFs python "{input.script}" \
          "{input.pep}" \
          --min_len {config[min_aa]} \
          --max_len {config[max_aa]} \
          --out_fasta "{output.smorfs_fa}" \
          --out_ids "{output.ids}"

        grep -F -f "{output.ids}" "{input.gff3}" > "{output.smorfs_gff3}"

        test -s "{output.smorfs_fa}"
        test -s "{output.ids}"
        test -s "{output.smorfs_gff3}"
        """


rule tx_to_genome_gtf:
    input:
        sample_gtf=f"{OUTDIR}/{{sample}}/stringtie/{{sample}}.gtf",
        smorfs_gff3=f"{OUTDIR}/{{sample}}/smorfs/{{sample}}.smorfs.gff3",
        script=config["tx_to_genome_py"]
    output:
        gtf=f"{OUTDIR}/{{sample}}/shortstop/{{sample}}.smorfs_shortstop.gtf"
    threads: 1
    shell:
        r"""
        set -euo pipefail
        mkdir -p "{OUTDIR}/{wildcards.sample}/shortstop"

        conda run --no-capture-output -n smORFs python "{input.script}" \
          --merged_gtf "{input.sample_gtf}" \
          --smorfs_gff3 "{input.smorfs_gff3}" \
          --out_gtf "{output.gtf}"

        test -s "{output.gtf}"
        """


rule shortstop_feature_extract:
    input:
        genome=config["genome_fa"],
        smorfs_gtf=f"{OUTDIR}/{{sample}}/shortstop/{{sample}}.smorfs_shortstop.gtf"
    output:
        done=f"{OUTDIR}/{{sample}}/shortstop/feature_extract.done"
    threads: config.get("threads_shortstop", 8)
    resources:
        mem_mb=16000,
        time="04:00:00"
    shell:
        r"""
        set -euo pipefail

        source /home/sbarber/miniconda3/etc/profile.d/conda.sh
        conda activate smORFs

        export LD_LIBRARY_PATH="$CONDA_PREFIX/lib"
        unset PYTHONPATH
        unset LD_PRELOAD || true

        sample_dir="{OUTDIR}/{wildcards.sample}/shortstop"
        mkdir -p "$sample_dir"
        cd "$sample_dir"
        mkdir -p shortstop_output

        python -m shortstop.cli feature_extract \
          --genome "{input.genome}" \
          --putative_smorfs_gtf "{input.smorfs_gtf}" \
          --outdir shortstop_output \
          --threads {threads}

        touch "{output.done}"
        """


rule shortstop_predict:
    input:
        genome=config["genome_fa"],
        smorfs_gtf=f"{OUTDIR}/{{sample}}/shortstop/{{sample}}.smorfs_shortstop.gtf",
        feat_done=f"{OUTDIR}/{{sample}}/shortstop/feature_extract.done"
    output:
        done=f"{OUTDIR}/{{sample}}/shortstop/predict.done"
    threads: config.get("threads_shortstop", 8)
    resources:
        mem_mb=16000,
        time="04:00:00"
    shell:
        r"""
        set -euo pipefail
        source /home/sbarber/miniconda3/etc/profile.d/conda.sh
        module purge || true
        conda activate smORFs

        unset PYTHONPATH
        unset LD_PRELOAD || true
        export LD_LIBRARY_PATH="$CONDA_PREFIX/lib:${{LD_LIBRARY_PATH:-}}"

        sample_dir="/storage/scratch01/users/sbarber/Workdir/results_shortstop/{wildcards.sample}/shortstop"
        cd "$sample_dir"

        shortstop predict \
        --genome "/storage/scratch01/users/sbarber/Workdir/GRCh38.primary_assembly.genome.fa" \
        --putative_smorfs_gtf "/storage/scratch01/users/sbarber/Workdir/results_shortstop/{wildcards.sample}/shortstop/{wildcards.sample}.smorfs_shortstop.gtf" \
        --outdir shortstop_output \
        --threads {threads} \
        2>&1 | tee shortstop_output/predict.run.log

        touch "/storage/scratch01/users/sbarber/Workdir/results_shortstop/{wildcards.sample}/shortstop/predict.done"

        """
